plugins {
	id 'java'
	id 'org.springframework.boot'
	id 'nebula.ospackage' version '9.1.1'
	id 'com.google.cloud.tools.jib' version '3.3.2'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation project(':rqms-api-general')
	implementation project(':rqms-api-offline')
	implementation project(':rqms-api-online')
	implementation project(':rqms-api-integration')
	/*implementation project(':rqms-app-general')
    implementation project(':rqms-app-offline')
    implementation project(':rqms-app-online')
    implementation project(':rqms-app-plugins')
    implementation project(':rqms-domain-offline')
    implementation project(':rqms-domain-online')
    implementation project(':rqms-domain-plugins')
    implementation project(':rqms-domain-plugins')*/
	implementation project(':rqms-infra')
}

jib {
	from {
		image = "openjdk:17-slim"
	}
	to {
		image = "rqms"
		tags = [System.getenv("GITHUB_SHA") == null ? "unknown" : System.getenv("GITHUB_SHA")]
	}
	container {
		mainClass = "com.raynigon.rqms.runable.RqmsApplication"
		ports = ["8080"]
		appRoot = "/rqms/"
		volumes = ["/data/"]
	}
}

ospackage {
	packageName = "rqms"
	release = 1
	user = "rqms"
	summary = "SUMMARY"
	url = "https://raynigon.com"
	packager = "Developer <development@raynigon.de>"
	vendor = "Raynigon"
	buildHost = 'https://github.com'

	customFields << [
			'Commit-ID' : System.getenv("GITHUB_SHA"),
			'Build-Job' : System.getenv("GITHUB_WORKFLOW"),
			'Build-Host': 'https://github.com',
			'Owner'     : 'Developer <development@raynigon.de>'
	]

	requires('coreutils')
	requires('openjdk-17-jre-headless')

	into '/usr/share/rqms'

	from(jar.outputs.files) {
		into '/usr/share/rqms/bin'
		rename 'rqms.*\\.jar', 'rqms.jar'
		fileMode 0755
	}

	from('src/dist/bash/bin') {
		into '/usr/share/rqms/bin'
		fileMode 0750
	}

	from('src/dist/config') {
		into '/etc/rqms/'
		fileMode 0444
	}

	from('src/dist/systemd') {
		into '/etc/systemd/system/'
	}
	postInstall file('src/dist/bash/deb/post-install.sh')
	preUninstall file('src/dist/bash/deb/pre-uninstall.sh')
	postUninstall file('src/dist/bash/deb/post-uninstall.sh')
}

buildRpm {
	dependsOn(tasks.getByName("build"))
}

buildDeb {
	dependsOn(tasks.getByName("build"))
}